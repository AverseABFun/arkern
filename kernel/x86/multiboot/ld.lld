OUTPUT_FORMAT(binary) /* get that ELF crap out of here! this is flat land! */
ENTRY(_start)  /* entry point */

SECTIONS
{
    /* code */
    . = 0x7E00;          /* loaded right after stage 1 */

    .text : {
        *(.start*)
        *(.text*)        /* add the default .text sections */
    }

    /* read-only data */
    .rodata : {
        *(.rodata*)
    }

    /* read-write data */
    .data : {
        *(.data*)
    }

    /* uninitialized data */
    .bss : {
        *(.bss*)
        *(COMMON)        /* common symbols go here too */
    }

    .stack (NOLOAD) : {
        . = ALIGN(16);
        PROVIDE(_stack_start = .);
        . += 0x10000;     /* 64KB stack */
        PROVIDE(_stack_end = .);
    }

    .heap (NOLOAD) : {
        . = ALIGN(16);
        PROVIDE(_heap_start = .);
        . += 0x10000;     /* 64KB heap */
        PROVIDE(_heap_end = .);
    }

    /DISCARD/ : {        /* discard unnecessary sections */
        *(.note*)
        *(.comment)
        *(.eh_frame)
    }
}
